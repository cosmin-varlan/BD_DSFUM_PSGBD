<h2> Carduri de memorie </h2>

<p>Cardurile de memorie se pot regasi pe diverse shielduri atasabile arduino-ului, in general cele ce necesita mai multe date pentru a functiona corect, date ce nu pot fi generate de catre placa Arduino.</p>
<p>Iata cateva proiecte interesante ce pot fi facute cu ajutorul cardurilor de memorie (poate va inspiram):</p>

<ul>
<li> Server web - in care fisierele HTML sa nu fie generate de catre Arduino ci sa fie stocate pe card. Eventual acestea pot fi modificate pe alocuri cu valori obtinute de la diversi senzori conectati la Arduino. Cum shieldurile Ethernet au un astfel de cititor de carduri MicroSD, realizarea unui astfel de proiect este super usoara.</li>
<li> Stocarea de imagini - majoritatea ecranelor au si un astfel de cititor de carduri. Pe el pot fi stocate imagini ce vor fi afisate pe ecran sau puteti sa generati imagini (in cazul in care ecranul are si o componenta tactila) pe care sa le stocati pe card.</li>
<li> Salvarea de fisiere de tip log - pentru a face diverse statistici referitoare la valorile senzorilor conectati.</li>
<li> Stocarea de melodii (genul celor facute acum doua laboratoare) si redarea lor pe dispozitive cu capabilitati sonore mai performante decat buzzerul pe care l-am utilizat ( https://www.sparkfun.com/products/10587 )</li>
<li> Salvarea starii curente a Arduinoului pentru a relua dintr-un anumit punct taskul curent (de exemplu stocarea starii unui joc pe care acesta il joaca cu partenerul uman).</li>
</ul>


<p>Evident ca exemplele pot continua. </p>


<h2> Utilizarea cardului SD </h2>

<p>Comunicarea cu cardul SD se face tot prin protocolul SPI despre care ati invatat in laboratorul precedent. De aceasta data insa, pentru selectarea cardului pinul ce trebuie sa fie setat pe low (pinul SS) este pinul 4.  Puteti reciti sectiunea "Comunicarea SPI" din laboratorul precedent pentru a va reaminti cum functioneaza comunicarea de tip SPI.</p>

<p>Informatiile despre biblioteca dedicata lucrului cu cardurile SD se gaseste la adresa: https://www.arduino.cc/en/Reference/SD</p>

<p>Biblioteca permite citirea si scrierea fisierelor pe cardul MicroSD daca acestea sunt formatate FAT16 sau FAT32. Ca o reminiscenta a FAT16, fisierele ce pot fi scrise/citite pot avea doar 8 caractere in nume si o extensie de maxim 3 caractere. In caz ca pe card aveti fisiere care depasesc aceasta dimensiune ele vor fi indexate cu ~nr. De exemplu, daca aveti un fisier "THEUNForgiven2.MP3" biblioteca il va vedea ca si "THEUNF~1.MP3".</p>

<p>Cateva informatii despre felul in care puteti folosi cardul MicroSD (cum ar trebui formatat) si ce shielduri sunt dotate cu acest card puteti gasi la adresa: https://www.arduino.cc/en/Reference/SDCardNotes</p>


<h2> Principalele functii din SD.h </h2>

<p>Functiile din SD.h vor fi impartite in 3 categorii: acces la nivel hardware, accesarea structurii de directoare si fisiere, accesarea continutului fisierelor:</p>

<h3> Acces la nivel hardware </h3>

<ul>
<li> SD.begin(ss) - pentru inceperea utilizarii cardului: seteaza pinul dat ca parametru pe LOW pentru ca dispozitivul SPI selectat sa fie cardul SD. In cazul nostru, pinul ss va avea valoarea 4 (pentru Shieldurile Ethernet). Daca operatia s-a incheiat cu succes va fi returnata valoarea true.</li>
</ul>


<h3> Accesarea structurii de directoare si fisiere </h3>

<ul>
<li> mkdir("nume_director") - creeaza directorul cu numele specificat. Daca parametrul continie mai multe directoare (de exemplu dir1/dir2/dir3) toate acestea vor fi create. Intoarce true daca a reusit.</li>
<li> rmdir("nume_director") - sterge directorul daca exista. Intoarce true daca a reusit.</li>
<li> open("nume_fisier", "tip_deschidere") - deschide un fisier de pe card. Tipul deschiderii poate fi FILE_READ sau FILE_WRITE. Returneaza o referinta catre fisierul deschis sau fals daca operatia nu s-a putut efectua.</li>
<li> close() - se foloseste referinta fisierului deschis anterior pentru a se inchide (ex. fis.close());</li>
<li> remove("nume_fisier") - sterge fisierul dat ca parametru. Intoarce true daca operatia s-a putut efectua.</li>
<li> exists("nume") - returneaza true daca fisierul sau directorul avand numele dat ca parametru exista.</li>
</ul>


<h3> Accesarea continutului fisierelor </h3>

<p>Considerand ca fis este o referinta catre fisierul deschis cu open, pentru acesta aveti o serie de comenzi dupa cum urmeaza:</p>
<h6> Comenzi pentru citire </h6>
<ul>
<li> fis.available() - returneaza numarul de bytes disponibili pentru a fi cititi din fisier.</li>
<li> fis.read(buffer, length) - citeste intr-un array denumit buffer o lungime de length bytes. Citirea urmatoare va citi urmatorii bytes.</li>
<li> fis.peek() - citeste caracterul curent fara sa avanseze la urmatorul.</li>
<li> fis.position() - returneaza pozitia curenta in fisier (sau pozitia caracterului ce va fi citit cu peek).</li>
<li> fis.seek() - sare la o anumita pozitie in fisier (urmatoarea citire se va face de la acea pozitie).</li>
</ul>


<h6> Comenzi pentru scriere </h6>
<ul>
<li> fis.write(buffer,length) - scrie datele din buffer in fisier. Length reprezinta numarul de elemente din buffer (caractere say bytes).</li>
<li> fis.print("some text") - scrie "some text" in fisier. Poate scrie string, char, int sau long, numerele sunt scrise cifra cu cifra.</li>
<li> fis.println("some text") - la fel dar sare pe randul urmator.</li>
<li> fir.flush() - se asigura ca datele trimise catre card s-au scris fizic pe acesta (s-ar putea sa fie pastrate intr-un buffer pana la scrierea efectiva si flush se asigura de aceasta scriere). Operatia de flush se face automat la inchiderea fisierului.</li>
</ul>


<h6> Comenzi diverse </h6>
<ul>
<li> fis.name() - returneaza numele fisierului.</li>
<li> fis.isDirectory() - returneaza true daca fisierul este un director</li>
<li> fis.openNextFile() - deschide urmatorul fisier si returneaza o referinta catre acesta, cand nu mai sunt fisiere, se poate reveni la primul cu :</li>
<li> fis.rewindDirectory();</li>
</ul>


<h2> Descarcati imaginea pentru card </h2>

<p><span style="font-weight:bold; background-color:yellow"> Descarcati imaginea cardului de [https://profs.info.uaic.ro/~vcosmin/pagini/resurse_arduino/ImagineCardSD/arduino_card.zip aici]. </span> Dezarhivati direct in radacina unui card formatat FAT32 sau FAT16.</p>

<h2> Exemplu ==</h2>

<p>Pentru ca probabil oricum nu ati citit ce e mai sus, sa vedem un exemplu ce ne ofera patru optiuni: sa verificam existenta unui fisier, sa il creem si sa scriem ceva in el, sa ii citim continutul si ultima optiune este sa il stergem:</p>



<pre><code class="language-c"> 
#include <SPI.h>
#include <SD.h>

void setup() {
  Serial.begin(9600);
  while (!Serial) {
    ; // va asteptam sa deschideti consola seriala
  }
  if (!SD.begin(4)) { // 4 este pinul SS al cardului SD
    Serial.println("Nu putem accesa cardul... aveti un card acolo ? este formatat FAT16 sau FAT32 ?");    
    return; // iese si nu mai face nimic
  }  
  Serial.println("Accesare card -> OK");
}

int opt = 1;
void loop() {
  if (opt>0)
  {
    Serial.println("\n\nIntroduceti optiunea: ");
    Serial.println("1 - Fisierul exista ?");
    Serial.println("2 - Crearea fisierului.");
    Serial.println("3 - Citirea si afisarea fisierului.");
    Serial.println("4 - Stergerea fisierului.");  
    opt = 0;
  }
  
  if (Serial.available())
  {
    opt = Serial.read()-'0';
  }

  if (opt==1){
     if(SD.exists("myFile.txt")) Serial.println("DA, exista");
     else Serial.println("NU l-am gasit");
  }

  if (opt==2){
    File myFile = SD.open("myFile.txt", FILE_WRITE);
    if (myFile){
      myFile.println("Salutari din Arduino.");
      myFile.close();
      Serial.println("Am creat fisierul!");
    }
  }


  if (opt==3){
    if (SD.exists("myFile.txt")){
      File myFile = SD.open("myFile.txt", FILE_READ);    
      while (myFile.available()) {
        Serial.write(myFile.read());
      }    
      myFile.close();
    }
    else
    {
      Serial.println("Fisier Inexistent");
    }
  }

  if(opt==4){
    if (SD.exists("myFile.txt")) {SD.remove("myFile.txt");Serial.println("Fisier sters.");}
    else Serial.println("Nu am ce sterge.");
  }

}
</code></pre>


<p>Puteti incerca si alte exemple disponibile in IDEul arduino-ului in sectiunea SD.</p>

<p>Un al doilea exemplu este aplicatia ASCII ART care va transforma un fisier in format BMP in format Ascii. Fisierul va fi citit de pe card si outputul (ascii) va fi dat in consola seriala. Din cauza dimensiunii limitate a consolei seriale recomandam ca imaginile sa nu depaseasca o rezolutie de 170x60 pixeli, trebuie sa fie in format BMP pe 24 biti si sa aiba culoarea negru ca background.</p>



<pre><code class="language-c">
#include <SPI.h>
#include <SD.h>

#define SDC_CS 4


uint16_t read16(File& f) 
{
  uint16_t result;
  ((uint8_t *)&result)[0] = f.read(); // LSB
  ((uint8_t *)&result)[1] = f.read(); // MSB
  return result;
}

uint32_t read24(File& f) 
{
  uint32_t result = 0;
  ((uint8_t *)&result)[0] = f.read(); // LSB
  ((uint8_t *)&result)[1] = f.read();
  ((uint8_t *)&result)[2] = f.read();
  return result;
}

uint32_t read32(File& f) 
{
  uint32_t result;
  ((uint8_t *)&result)[0] = f.read(); // LSB
  ((uint8_t *)&result)[1] = f.read();
  ((uint8_t *)&result)[2] = f.read();
  ((uint8_t *)&result)[3] = f.read(); // MSB
  return result;
}

char get_color_specific_char(unsigned int pixel)
{
  //o culoare in format rgb este cu atat mai apropiata de componenta care o defineste cu cat
  //restul sunt obligatoriu mai mici
  //vom nota orice culoare care are byte-ul R mai mare ca restul cu R
  //orice culoare are byte-ul G mai mare ca restul cu G
  //si orice culoare care are byte-ul B mai mare ca restul cu B
  //nuantele gray in care R=G=B, vor fi notate cu N
  //iar culoarea alb cu ' ' (spatiu)
  unsigned char r,g,b,i,minim,k;
  unsigned int temp=pixel;
  char folosite[16]={'x','@','-','+','$','%','^','&','*',';','/',' ','?','#','~','.'};
  //culorile negru si alb sunt spatiu
  unsigned char cnr[16];
  unsigned char rr[16]={0  ,0  ,0  ,0  ,128,128,128,192,128,0  ,0  ,0  ,255,255,255,255};
  unsigned char gg[16]={0  ,0  ,128,128,0  ,0  ,128,192,128,0  ,255,255,0  ,0  ,255,255};
  unsigned char bb[16]={0  ,128,0  ,128,0  ,128,0  ,192,128,255,0  ,255,0  ,255,0  ,255};
  // 4 bit colors     = bk  bl  gr  aq  rd  pl  yl  wt  gy  lbl lgr laq lrd lpl lyl bwt 
  b=temp;
  temp>>=8;
  g=temp;
  temp>>=8;
  r=temp;
  
  //compute 16 custom numbers defining colors
  for (i=0;i<16;i++)
    cnr[i]=(abs(r-rr[i])+abs(g-gg[i])+abs(b-bb[i]))/3;
  
  minim=cnr[0];
  k=0;
  
  //compute most appropiate color
  for (i=1;i<16;i++)
  {
    if (cnr[i]<minim)
    {
      minim=cnr[i];
      k=i;
    }
  }  
  return folosite[k];
}

void PrintBMPASCII(char *fname)
{
  File bmpFile;
  unsigned int bmpWidth, bmpHeight, start_pos, linesize, pos, txtpos, linecount, pixel;
  unsigned int i,j;
    
  if ((bmpFile = SD.open(fname)) == NULL) 
  {
    Serial.println("File not found");
    return;
  }   

  if (read16(bmpFile) == 0x4D42) 
  {
    read32(bmpFile);      
    read32(bmpFile);     
    start_pos = read32(bmpFile);  // Start of image data
    read32(bmpFile);              // Dummy read to throw away and move on
    bmpWidth  = read32(bmpFile);  // Image width
    bmpHeight = read32(bmpFile);  // Image height

    linesize=bmpWidth*3+bmpWidth%4;

    Serial.print("Width = ");
    Serial.println(bmpWidth);
    Serial.print("Height = ");
    Serial.println(bmpHeight);  
    Serial.print("Start = ");
    Serial.println(start_pos);  
    Serial.print("LineSize = ");
    Serial.println(linesize);          

    if ((read16(bmpFile) == 1) && (read16(bmpFile) == 24) && (read32(bmpFile) == 0)) 
    {      
      for (i=0;i<bmpHeight;i+=2)
      {
        bmpFile.seek(start_pos+linesize*(bmpHeight-1-i));
        for (j=0;j<bmpWidth;j++)
        {
          pixel=read24(bmpFile);
          Serial.print(get_color_specific_char(pixel));
        }
        Serial.println("");
      }       
    }
    else
      Serial.println("BMP format nesuportat!");
  }  

  bmpFile.close();
}

void setup() 
{
  Serial.begin(9600);
  Serial.print("Initializing SD card...");
  if (!SD.begin(SDC_CS)) 
  {
    Serial.println(" [FAILED]");
    return;
  }
  Serial.println(" [OK]");  

  PrintBMPASCII("/AsciiArt/tree.bmp");
  PrintBMPASCII("/AsciiArt/heart.bmp");
  PrintBMPASCII("/AsciiArt/earth.bmp");
  PrintBMPASCII("/AsciiArt/mouse.bmp");
  PrintBMPASCII("/AsciiArt/alien.bmp");
  PrintBMPASCII("/AsciiArt/stars.bmp");  
  PrintBMPASCII("/AsciiArt/Spidey.bmp");    

  Serial.println("done!");
}

void loop() {

}
</code></pre>


<h2> Exercitii </h2>

<p>Alegeti una din cel 2 probleme propuse (!!!puteti rezolva ambele, dar primiti punctaj doar pentru una!!!)</p>

<h6> Problema 1 [20pt] </h6>

<p>Realizati un CLI (Comand Line Interface) implementand urmatoarele comenzi ce vor fi date prin intermediul consolei seriale:</p>
<ul>
<li> cd - change directory: va schimba directorul curent cu cel dat ca parametru. Directorul dat ca parametru poate fi absolut (pornind din radacina cardului ca de exemplu "/Muzica/Mod" sau relativ la direcotul curent ca de exemplu "./Mod". In final se va afisa directorul curent in consola seriala. [comanda "cd ." va afisa directorul curent]</li>
<li> mkdir - se va crea un fisier cu numele dat ca parametru (de exemplu comanda "mkdir myFolder") va crea direcotul myFolder in directorul curent.</li>
<li> rmdir - sterge directorul dat ca parametru daca acesta exista.</li>
<li> ls - afisarea fisierelor si directoarelor din directorul curent impreuna cu data crearii lor si a dimensiunilor.</li>
<li> cat - afisarea continutului fisierului dat ca parametru (de exemplu "cat nume_fisier.txt" va afisa in consola seriala contonutul fisierului text dat ca parametru). daca parametrul dat ca parametru este de tipul BMP va afisa transformarea lui in imagine textuala ca in exemplul dat mai sus (puteti utiliza codul nostru dar trebuie sa il integrati in aplicatia voastra).</li>
<li> echo "something to write into a file" > file.txt va crea fisierul file.txt (sau il va suprascrie daca exista) si va adauga in el continutul dat intre ghilimele.</li>
<li> find - cauta recursiv pornind din directorul curent un fisier dat ca parametru (de exemplu daca directorul curent este radacina si dau comanda find myFile.txt si acesta se afla in /dir1/dir2/ , imi va afisa "/dir1/dir2/myFile.txt"; evident, cautarea va continua pentru gasirea tuturor fisierelor cu acel nume).</li>
<li> cp cale1/fisier cale2 copie fisierul "fisier" aflat in directorul "cale1" in directorul "cale2".</li>
<li> [https://en.wikipedia.org/wiki/Cyclic_redundancy_check CRC] - calcularea CRC-4 (cod cu redundanta ciclica) avand drept polinom generator X<sup>4</sup>+X<sup>2</sup>+X+1.CRCul va fi calculat doar pe continutul fisierului dat ca parametru.</li>
</ul>


<p>Puncte: cd = 2pt,  mkdir = 1pt,  rmdir = 1pt,  ls = 2pt, cat = 4pt, echo = 2pt, find = 3pt, cp = 1pt, CRC = 4pt !</p>

<h6> Problema 2 [20pt] </h6>
<p>Pe scheletul aplicatiei prezentate aici: [https://www.instructables.com/id/Audio-Player-Using-Arduino-With-Micro-SD-Card/]</p>
<p>Scrieti un wav-player format din 2 componente:</p>
<ul>
<li> O aplicatie PC (Python/C/C++/PowerShell) care sa tina loc de consola seriala si cu care sa trimiteti comenzi catre Arduino</li>
<li> O aplicatie pe Arduino care sa faca PLAY la un WAV selectat (puteti folosi propriile casti sau folositi niste buzzere)</li>
</ul>


<p>Detalii tehnice pentru aplicatie:</p>
<ul>
<li> Creati o comanda MAKE care sa construiasca un album audio (se va scana cardul recursiv si se va construi un fisier playlist cu toate WAV-urile gasite)</li>
<li> Creati o comanda PLAY care primeste un parametru BOOL (play linear / play random) si care incepe sa cante melodiile din playlist</li>
<li> Creati o comanda STOP care sa permita intreruperea oricarei melodii in desfasurare</li>
<li> Creati o comanda NEXT care sa treaca fortat la urmatoarea melodie, indiferent daca este o melodie in desfasurare sau nu</li>
<li> Creati o comanda PREV care sa treaca fortat la precedenta melodie, indiferent daca este o melodie in desfasurare sau nu</li>
<li> Creati o comanda LIST care sa afiseze toate melodiile din PLAYLIST, impreuna cu pathul full catre melodie si durata acesteia</li>
</ul>


<p>Puncte: NEXT = 2p, PREV = 2p, LIST = 4p, MAKE = 4p, PLAY = 4p, STOP = 4p</p>

<p><span style="color:red">Nota: Aceasta tema este mai degraba un mini-proiect. Din acest motiv vom permite oricarui doritor care a luat macar 5 puncte sa prezinte problema integral dupa saptamana de partiale.</span></p>


<p><pre></p>
<p>                 ++++++                                              </p>
<p>           ++---++--++-----++++                     +++              </p>
<p>         +---+-++          ++++---++              +-+++              </p>
<p>       ++--+++                    ++-+ +--++++  ++--+++              </p>
<p>       ---+++                           +---++-----++++              </p>
<p>      ++--+++                            +----++--++++++             </p>
<p>      -+--+++                             +-----+-++++++++++         </p>
<p>      -+--+++                            +-+++++--------+++++++      </p>
<p>      +-+-+++                           ++++------++-----++          </p>
<p>      +-+--+++                        +-+----------+++               </p>
<p>       +-+-++++                      ++++++++ ++---+-+               </p>
<p>       +--+-++++           +++                  +---++ ++            </p>
<p>        +-----++         +-+++                    +-+  +-+           </p>
<p>            +++-+      ++-++++                     ++   +-+          </p>
<p>                      +---++++                           +-+         </p>
<p>       ---++--+++   +----+++++                            +-+        </p>
<p>        +----+----------++++++                             +++       </p>
<p>         +------+-------+++++++                            +-++      </p>
<p>          +-------++----+++++++++++                         +-++     </p>
<p>           +---------+-+++++++++++++++++                    +-++     </p>
<p>            +--------++--++++++++++++++++++                 +-++     </p>
<p>           +--++--++----+--------------+                    +--++    </p>
<p>          +-++-++--------++--------+                        +--++    </p>
<p>         +-+++-----------+--+-+     +--++                   ++-++    </p>
<p>        +-+---------------+---  ++++-------++              ++-++     </p>
<p>       +------++++ ++------+-+  +----+++-------++++     ++++--++     </p>
<p>      +++            ++-----++   ++-------+++----------------++      </p>
<p>                       +----++       ++---------+++++++++++-+        </p>
<p>                         +---+             ++------------+           </p>
<p>                           +-+                                                                                </p>
<p>                                                                                      </p>
<p></pre></p>


<p>Si un ultim cod sursa (pentru ca am observat ca nu prea stiti sa lucrati cu siruri de caractere) - dupa ce l-ati pus pe Arduino, scrieti in consola seriala un sir de cuvinte despartite prin spatiu:</p>


<pre><code class="language-c">
void setup() {
  Serial.begin(9600);
}
void loop() {
    if(Serial.available() > 0)
    {
        String s = Serial.readString();              
        Serial.println("Am receptionat: " + s);                 
        // daca vreti puteti sa va jucati cu obiectul de tip String
        // aveti instructiunile aici: https://www.arduino.cc/reference/en/language/variables/data-types/stringobject/

        // sau:

        char sir[1000];
        s.toCharArray(sir, 1000); // converteste in sir de caractere
        sir[s.length()]='\0'; // adaugam finalul sirului
        Serial.println(strlen(sir)); // afisam lungimea sirului
        Serial.println(sir);  // si sirul efectiv

        // si daca vreti sa il spargeti in cuvinte delimitate de spatiu:
        char cuvinte[20][20]; // formati o matrice
        int poz=0, cuvant=0; // incepeti cu primul cuvant, caracterul 0
        
        for(int i=0; i<strlen(sir); i++, poz++)
          if (sir[i]==' ') // si cand ati intalnit un spatiu
          {
            cuvinte[cuvant][poz]='\0';  // adaugati sfarsit de cuvant in matrice
            cuvant++;                   // si sariti la urmatorul cuvant
            poz=-1;                     // la prima litera
          }
          else
          cuvinte[cuvant][poz] = sir[i]; // daca nu-i spatiu, adaugati litera la ultimul cuvant

          cuvinte[cuvant][poz]='\0';  // ultimul cuvant din matrice trebuie sa aiba si el terminatorul \0
          
        for(int i=0; i<=cuvant; i++)   // si afisati pe rand cuvintele
          Serial.println(cuvinte[i]);                         
    }
}
</code></pre>







<pre><code class="language-c">
#include <SPI.h>
#include "SdFat.h"
#include "sdios.h"

const uint8_t chipSelect = 4;

SdFat sd;


void setup() {
  Serial.begin(9600);

  while (!Serial) {
    SysCall::yield();
  }
  delay(1000);

  if (!sd.begin(chipSelect, SD_SCK_MHZ(50))) {
    sd.initErrorHalt();
  }
}


char cale[255]="/";
char temp[255];
int opt=1;

void loop() {
 
    if (opt>0)
    {
      Serial.println("\n\nIntroduceti optiunea: ");
      Serial.println("1 - LS (listeaza directorul curent)");
      Serial.println("2 - CD (change directory)");
      Serial.println("3 - Citirea si afisarea fisierului.");
      Serial.println("4 - Stergerea fisierului.");  
      opt = 0;
    }
  
    while (Serial.available())
    {
        opt = Serial.read()-'0';
    }
  
   if (opt==1)
   { 
      listFiles(cale);
   }
   
   if (opt==2)
   { 
      int gotIt = 0;
      while(!gotIt)
      {
        while (Serial.available())
        {
           String s = Serial.readString();   
           //Serial.println(s);
           int i;
           for(i=0; s[i]; i++)
              temp[i]=s[i];
           temp[i]=0;
           
           //Serial.println(strlen(temp));           
           // DE TRATAT .. SI .
           if (strequ(temp, ".")) break;
           if (strequ(temp, ".."))
           {
             cale[strlen(cale)-1]=0;
             int j=strlen(cale)-1;
             while(cale[j]!='/') cale[j--]=0;
           }
           else
           {
              strcat(cale,temp);
              strcat(cale,"/");
           }
           Serial.println(cale);           
           gotIt=1;
        }
      }
   }
}


void listFiles(char cale[]){
  SdFile dir;
  SdFile fisier;

  if (!dir.open(cale)) {
    Serial.println("Nu avem directorul acesta");
  }
  else
  {
     while (fisier.openNext(&dir, O_RDONLY))
     {
         char buf[255];
         fisier.getName(buf,255);
         if (fisier.isDir()) Serial.print("d ");
                             Serial.print("f ");
         Serial.println(buf);
         fisier.close();
     }
  }
}




int strlen(char s[]){
  int i=0;
  while(s[i])i++;
  return i;
}

void strcat(char dest[], char sursa[])
{
   int i=strlen(dest);
   int j=0;
   while(sursa[j]){
     dest[i]=sursa[j];
     i++;
     j++;
     if (i>255) break;
   }
   dest[i]=0;  
}

int strequ(char s1[], char s2[])
{
  int i=0;
  while(s1[i]==s2[i] && s1[i]!=0) i++;
  if (s1[i]==s2[i] && s1[i]==0) return 1;
  return 0;
}

</code></pre>

